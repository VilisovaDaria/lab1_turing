[ТЗ: Сортировка выбором массива чисел in-place. Ответ содержится в 100 - 105 ячейках]                            





























T 106 S
[loop1:]T 30 S [обнуление акк.]
A 98 S [загрузка в акк. количества неотсортированных элементов <length> ]
S 97 S [уменьшаем на 1 <c1> ]
T 98 S [перезапись счетчика <length>]
A 96 S [загрузка в акк. константы 0 <c0>]
U 6 S [формируем счетчик i]
S 98 S [уменьшаем значение на <length>]
E 93 S [if A>=0 завершение программы <exit>]
T 30 S [обнуление акк.]
A 99 S [загрузка в аккумулятор адреса 0-го элемента массива <addr>]
U 0 S [записываем значение в рабочую ячейку 0]
U 1 S [записываем значение в рабочую ячейку 1]
L 0 L [сдвиг акк. на 1 разряд влево]
A 95 S [прибавляем код инструкции A 0 S, с нулевым полем адреса]
T 47 S [запись сформированной инструкции в ячейку 47]
A 0 S [загрузка в аккумулятор ячейки N массива]
T 2 S [запись этого значения в рабочую ячейку 2, обнуление аккумулятора]
[loop2:] T 30 S [обнуление акк.]
A 1 S [загрузка адреса элемента N массива]
A 97 S [увеличиваем на константу 1 <c1>]
U 1 S [перезаписываем значение адреса]
L 0 L [сдвиг акк. на 1 разряд влево]
A 95 S [прибавляем код инструкции A 0 S, с нулевым полем адреса]
T 56 S [запись сформированной инструкции в ячейку 56]
A 0 S [помещаем в акк. N+i элемент массива]
T 3 S [записываем в 3 ячейку]
A 6 S [формируем второй счетчик для цикла loop2]
A 97 S [увеличиваем i на константу 1 <c1>]
U 6 S [перезаписываем значение i]
S 98 S [уменьшаем на количество неотсортированных элементов <length>]
E 75 S [if A >= 0 выйти из цикла loop2, продолжить цикл loop1]
T 30 S [обнуление акк.]
A 2 S [поместили в акк. N элемент]
S 3 S [сравниваем с элементом N+i]
E 49 S [if N >= N+i,  берем следующий элемент в цикле loop2]
T 30 S [обнуление акк.]
A 3 S [считываем N+i элемент массива]
T 2 S [записываем в ячейку для N элемента]
A 1 S [считываем адрес N+i элемента]
T 0 S [записываем в ячейку для адреса N элемента]
A 3 S [помещаем в акк. N+i элемент]
S 3 S [очистили ячейку]
E 49 S [повтор действий цикла loop2]
[loop1:]T 30 S [обнуление акк.]
A 2 S [загрузка в акк. максимального элемента неотсортированной части массива, полученного из цикла loop2]
S 3 S [сравниваем с последним элементом неотсортированной части массива]
G 32 S [если последний больше наибольшего элемента, то продолжаем цикл loop1, так как перестановка элементов не требуется]
T 30 S [обнуление акк.]
A 0 S [помещаем в акк. адрес наибольшего элемента неотсортированной части массива]
L 0 L [сдвиг акк. на 1 разряд влево]
A 94 S [прибавляем код инструкции T 0 S]
T 85 S [запись сформированной инструкции в ячейку 85]
A 3 S [помещаем в акк. последний элемент неотсортированной части массива]
T 0 S [записываем элемент на место наибольшего элемента]
A 1 S [помещаем в акк. адрес последнего элемента массива]
L 0 L [сдвиг акк. на 1 разряд влево]
A 94 S [прибавляем код инструкции T 0 S]
T 91 S [запись сформированной инструкции в ячейку 91]
A 2 S [помещаем в акк. наибольший элемент неотсортированной части массива]
T 0 S [записываем элемент на место последнего элемента]
E 32 S [повторяем все операции]
[exit:] Z 0 S [останов]
[шаблоны инструкций:] T 0 S [инструкция для записи в ячейки по адресу]
A 0 S [инструкция для чтения значения из ячейки по адресу]
[const c0:] P 0 S [0]
[const c1:] P 0 L [1]
[length:] P 3 S [6]
[addr:] P 50 S [100]
[array:] P 4 S [8]
P 5 L [11]
P 8 S [16]
P 4 L [9]
P 41 L [83]
P 2 L [5]          
